# Copyright (c) 2023 Yuki Kimoto
# MIT License

class Getopt::Long {
  version "0.001";
  
  use Array;
  use Regex;
  use Fn;
  use StringList;
  use Getopt::Long::Spec;
  
  static method GetOptionsFromArray : string[] ($argv : string[], $values_h : Hash, $spec_strings : string[]) {
    
    unless ($values_h) {
      die "\$values_h must be defined.";
    }
    
    my $specs_h = &parse_spec_strings($spec_strings);
    
    my $new_argv = &parse_argv($argv, $values_h, $specs_h);
    
    return $new_argv;
  }
  
  private static method parse_spec_strings : Hash of Getopt::Long::Spec ($spec_strings : string[]) {
    
    my $specs_h = Hash->new;
    
    my $spec_strings_length = @$spec_strings;
    
    for (my $i = 0; $i < $spec_strings_length; $i++) {
      my $spec_string = $spec_strings->[$i];
      
      my $spec_re = Regex->new("^(\w+(?:\|\w+))*(?:=([\w])(\@)?)?$");
      
      if (my $match = $spec_re->match($spec_string)) {
        my $names_str = $match->cap1;
        my $type_char = $match->cap2;
        my $is_array_char = $match->cap3;
        
        my $names = Fn->split("|", $names_str);
        
        unless ($type_char eq "s" || $type_char eq "i" || $type_char eq "f") {
          die "The type \"$type_char\" in the spec is not available.";
        }
        
        my $spec = Getopt::Long::Spec->new;
        
        my $is_primary_name = 1;
        
        for my $name (@$names) {
          $spec->{name} = $name;
            
          if ($is_primary_name) {
            $spec->{primary_name} = $name;
            
            $is_primary_name = 0;
          }
          else {
            $spec->{primary_name} = $spec->{primary_name};
          }
          
          my $is_array = 0;
          if ($is_array_char) {
            $is_array = 1;
          }
          
          $spec->{is_array} = (byte)$is_array;
          
          my $type = Getopt::Long::Spec->TYPE_BOOL;
          if ($type_char eq "s") {
            $type = Getopt::Long::Spec->TYPE_STRING;
          }
          elsif ($type_char eq "i") {
            $type = Getopt::Long::Spec->TYPE_INT;
          }
          elsif ($type_char eq "f") {
            $type = Getopt::Long::Spec->TYPE_DOUBLE;
          }
          
          my $found_spec = $specs_h->get($name);
          
          if ($found_spec) {
            die "The option name \"$name\" must be given onece.";
          }
          
          $specs_h->set($name => $spec);
        }
      }
      else {
        die "The spec \"$spec_string\" is invalid.";
      }
    }
    
    return $specs_h;
  }
  
  private static method parse_argv : string[] ($argv : string[], $values_h : Hash, $specs_h : Hash of Getopt::Long::Spec) {
    my $new_argv_list = StringList->new;
    
    my $argv_length = @$argv;
    
    my $parse_start = 1;
    
    for (my $argv_index = 0; $argv_index < $argv_length; $argv_index++) {
      my $argv = $argv->[$argv_index];
      
      my $argv_number = $argv_index + 1;
      
      if ($parse_start) {
        
        unless ($argv) {
          die "The ${argv_number}th argument of $argv must be defined.";
        }
        
        my $is_option_start = 0;
        
        my $option_start_re = Regex->new("^--?");
        
        if ($argv eq "--") {
          $parse_start = 0;
          next;
        }
        
        my $option_re = Regex->new("--?(\w+)(?:=([\w\.\-]+))?");
        
        if (my $match = $option_re->match($argv)) {
          
          my $option_name = $match->cap1;
          
          my $option_value = $match->cap2;
          
          my $found_spec = (Getopt::Long::Spec)$specs_h->get($option_name);
          
          if ($found_spec) {
            my $primary_name = $found_spec->{primary_name};
            
            if ($found_spec->{type} == Getopt::Long::Spec->TYPE_BOOL) {
              $values_h->set($primary_name, 1);
            }
            else {
              unless ($option_value) {
                if ($argv_index + 1 < $argv_length) {
                  $option_value = $argv->[$argv_index + 1];
                }
              }
              
              unless ($option_value) {
                die "The value of the option \"$option_name\" is not found.";
              }
              
              my $is_array = $found_spec->{is_array};
              
              if ($found_spec->{type} == Getopt::Long::Spec->TYPE_STRING) {
                if ($is_array) {
                  my $current_values = (string[])$values_h->get($primary_name);
                  
                  $current_values = Array->merge_string($current_values, [$option_value]);
                  
                  $values_h->set($primary_name, $current_values);
                }
                else {
                  $values_h->set($primary_name, $option_value);
                }
              }
              elsif ($found_spec->{type} == Getopt::Long::Spec->TYPE_INT) {
                my $optiosn_value_int = Fn->to_int($option_value);
                
                if ($is_array) {
                  my $current_values = (Int[])$values_h->get($primary_name);
                  
                  $current_values = (Int[])Array->merge_object($current_values, [(Int)$optiosn_value_int]);
                  
                  $values_h->set($primary_name, $current_values);
                }
                else {
                  $values_h->set($primary_name, $optiosn_value_int);
                }
              }
              elsif ($found_spec->{type} == Getopt::Long::Spec->TYPE_DOUBLE) {
                my $optiosn_value_double = Fn->to_double($option_value);
                
                if ($is_array) {
                  my $current_values = (Double[])$values_h->get($primary_name);
                  
                  $current_values = (Double[])Array->merge_object($current_values, [(Double)$optiosn_value_double]);
                  
                  $values_h->set($primary_name, $current_values);
                }
                else {
                  $values_h->set($primary_name, $optiosn_value_double);
                }
              }
              else {
                die "Unexpected Error.";
              }
            }
          }
          else {
            die "The option \"$option_name\" is not available.";
          }
        }
        else {
          die "The option \"$argv\" is an invalid format.";
        }
      }
      else {
        $new_argv_list->push($argv);
      }
    }
    
    my $new_argv = $new_argv_list->to_array;
    
    return $new_argv;
  }
  
  # use Getopt::Long qw(:config posix_default no_ignore_case gnu_compat);
}
